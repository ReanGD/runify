// Lexical part

_digit : '0'-'9' ;

int64 : '1'-'9' {_digit} ;

!whitespace : ' ' | '\t' | '\n' | '\r' ;

// Syntax part

<<
import (
	"github.com/ReanGD/runify/server/provider/calculator/ast"
)
>>

Calc : Expr0Lvl;

Expr0Lvl :
		Expr0Lvl "+" Expr1Lvl	<< ast.BinaryExpr($0, $2, $1) >>
	|   Expr0Lvl "-" Expr1Lvl	<< ast.BinaryExpr($0, $2, $1) >>
	|	Expr1Lvl
;

Expr1Lvl :
		Expr1Lvl "*" Expr2Lvl	<< ast.BinaryExpr($0, $2, $1) >>
	|	Expr1Lvl "/" Expr2Lvl	<< ast.BinaryExpr($0, $2, $1) >>
	|	Expr2Lvl
;

Expr2Lvl :
		"-" Expr2Lvl			<< ast.UnaryExpr($1, $0) >>
	|	"+" Expr2Lvl			<< ast.UnaryExpr($1, $0) >>
	|	Expr3Lvl
;

Expr3Lvl :
		"(" Expr0Lvl ")"		<< $1, nil >>
	|	int64					<< ast.NewValueFromToken($0) >>
;
